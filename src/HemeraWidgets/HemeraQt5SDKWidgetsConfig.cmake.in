
if (CMAKE_VERSION VERSION_LESS 2.8.3)
    message(FATAL_ERROR "Hemera Qt SDK requires at least CMake version 2.8.3")
endif()

get_filename_component(_hemeraQtSDKWidgets_install_prefix @CMAKE_INSTALL_PREFIX@ ABSOLUTE)

# For backwards compatibility only. Use HemeraQt5SDKWidgets_VERSION instead.
set(HemeraQt5SDKWidgets_VERSION_STRING @HEMERA_QT5_SDK_VERSION@)

set(HemeraQt5SDKWidgets_LIBRARIES HemeraQt5SDK::Widgets)

if (NOT TARGET HemeraQt5SDK::Widgets)

    set(_HemeraQt5SDKWidgets_OWN_INCLUDE_DIRS @WIDGETS_INCLUDE_DIRS@)
    set(HemeraQt5SDKWidgets_INCLUDE_DIRS ${_HemeraQt5SDKWidgets_OWN_INCLUDE_DIRS})

    set(HemeraQt5SDKWidgets_DEFINITIONS "")
    set(HemeraQt5SDKWidgets_COMPILE_DEFINITIONS "")

    set(_HemeraQt5SDKWidgets_MODULE_DEPENDENCIES "Core")

    set(_HemeraQt5SDKWidgets_FIND_DEPENDENCIES_REQUIRED)
    if (HemeraQt5SDKWidgets_FIND_REQUIRED)
        set(_HemeraQt5SDKWidgets_FIND_DEPENDENCIES_REQUIRED REQUIRED)
    endif()
    set(_HemeraQt5SDKWidgets_FIND_DEPENDENCIES_QUIET)
    if (HemeraQt5SDKWidgets_FIND_QUIETLY)
        set(_HemeraQt5SDKWidgets_DEPENDENCIES_FIND_QUIET QUIET)
    endif()
    set(_HemeraQt5SDKWidgets_FIND_VERSION_EXACT)
    if (HemeraQt5SDKWidgets_FIND_VERSION_EXACT)
        set(_HemeraQt5SDKWidgets_FIND_VERSION_EXACT EXACT)
    endif()

    foreach(_module_dep ${_HemeraQt5SDKWidgets_MODULE_DEPENDENCIES})
        if (NOT HemeraQt5SDK${_module_dep}_FOUND)
            find_package(HemeraQt5SDK${_module_dep}
                ${HemeraQt5SDKWidgets_VERSION_STRING} ${_HemeraQt5SDKWidgets_FIND_VERSION_EXACT}
                ${_HemeraQt5SDKWidgets_DEPENDENCIES_FIND_QUIET}
                ${_HemeraQt5SDKWidgets_FIND_DEPENDENCIES_REQUIRED}
                PATHS "${CMAKE_CURRENT_LIST_DIR}/.." NO_DEFAULT_PATH
            )
        endif()

        if (NOT HemeraQt5SDK${_module_dep}_FOUND)
            set(HemeraQt5SDKWidgets_FOUND False)
            return()
        endif()

        list(APPEND HemeraQt5SDKWidgets_INCLUDE_DIRS "${HemeraQt5SDK${_module_dep}_INCLUDE_DIRS}")
        list(APPEND HemeraQt5SDKWidgets_DEFINITIONS ${HemeraQt5SDK${_module_dep}_DEFINITIONS})
        list(APPEND HemeraQt5SDKWidgets_COMPILE_DEFINITIONS ${HemeraQt5SDK${_module_dep}_COMPILE_DEFINITIONS})
        list(APPEND HemeraQt5SDKWidgets_EXECUTABLE_COMPILE_FLAGS ${HemeraQt5SDK${_module_dep}_EXECUTABLE_COMPILE_FLAGS})
    endforeach()
    list(REMOVE_DUPLICATES HemeraQt5SDKWidgets_INCLUDE_DIRS)
    list(REMOVE_DUPLICATES HemeraQt5SDKWidgets_DEFINITIONS)
    list(REMOVE_DUPLICATES HemeraQt5SDKWidgets_COMPILE_DEFINITIONS)
    if (HemeraQt5SDKWidgets_EXECUTABLE_COMPILE_FLAGS)
        list(REMOVE_DUPLICATES HemeraQt5SDKWidgets_EXECUTABLE_COMPILE_FLAGS)
    endif()

    set(_HemeraQt5SDKWidgets_LIB_DEPENDENCIES "HemeraQt5SDK::Core Qt5::Widgets Qt5::Declarative")

    # Target management
    include("${CMAKE_CURRENT_LIST_DIR}/HemeraQt5SDKWidgetsTargets.cmake")

    set_property(TARGET HemeraQt5SDK::Widgets PROPERTY
        INTERFACE_INCLUDE_DIRECTORIES ${_HemeraQt5SDKWidgets_OWN_INCLUDE_DIRS})
    set_property(TARGET HemeraQt5SDK::Widgets PROPERTY
        INTERFACE_COMPILE_DEFINITIONS ${HemeraQt5SDKWidgets_COMPILE_DEFINITIONS})

endif()
